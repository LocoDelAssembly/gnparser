package grammar

type Engine Peg {
  BaseEngine
}

SciName <- _? Name Tail !.

Tail <- ((_ / ';' / ',') .*)?

Name <- NamedHybrid / HybridFormula / SingleName

HybridFormula <- SingleName (_ (HybridFormulaPart / HybridFormulaFull))+

HybridFormulaFull <- HybridChar (_ SingleName)?

HybridFormulaPart <- HybridChar _ SpeciesEpithet (_ InfraspGroup)?

NamedHybrid <- NamedGenusHybrid / NamedSpeciesHybrid

NamedSpeciesHybrid <- GenusWord _ HybridChar _? SpeciesEpithet

NamedGenusHybrid <- HybridChar _? SingleName

SingleName <- NameComp / NameApprox / NameSpecies / NameUninomial

NameUninomial <- UninomialCombo / Uninomial

NameApprox <- GenusWord (_ SpeciesEpithet)? _ Approximation ApproxNameIgnored

NameComp <- GenusWord _ Comparison (_ SpeciesEpithet)?

NameSpecies <- GenusWord (_? (SubGenus / SubGenusOrSuperspecies))?
               _ SpeciesEpithet (_ InfraspGroup)?

GenusWord <- (AbbrGenus / UninomialWord) !(_ AuthorWord)

InfraspGroup <- InfraspEpithet (_ InfraspEpithet)?  (_ InfraspEpithet)?

InfraspEpithet <- (Rank _?)? !(AuthorEx) Word  (_ Authorship)?

SpeciesEpithet <- !(AuthorEx) Word (_? Authorship)?

Comparison <- ('cf' '.'?)

Rank <- RankForma / RankVar / RankSsp / RankOther / RankOtherUncommon

RankOtherUncommon <- ('*' / 'nat' / 'f.sp' / 'mut.') &(SpaceCharEOI)

RankOther <- ('morph.' / 'nothosubsp.' / 'convar.' / 'pseudovar.' / 'sect.' /
  'ser.' / 'subvar.' / 'subf.' / 'race' / 'α' / 'ββ' / 'β' / 'γ' / 'δ' / 'ε' /
  'φ' / 'θ' / 'μ' / 'a.' / 'b.' / 'c.' / 'd.' / 'e.' / 'g.' / 'k.' / 'pv.' /
  'pathovar.' / ('ab.' (_? 'n.')?) / 'st.') &(SpaceCharEOI)

RankVar <- 'variety' / '[var.]' / 'nvar.' / ('var' (&(SpaceCharEOI) / '.'))

RankForma <- ('forma' / 'fma' / 'form' / 'fo' / 'f') (&(SpaceCharEOI) / '.')

RankSsp <- ('ssp' / 'subsp') (&(SpaceCharEOI) / '.')

SubGenusOrSuperspecies <- '(' _? NameLowerChar+ _? ')'

SubGenus <- '(' _? UninomialWord _? ')'

UninomialCombo <- UninomialCombo1 / UninomialCombo2

UninomialCombo1 <- UninomialWord _? SubGenus (_? Authorship)?

UninomialCombo2 <- Uninomial _ RankUninomial _ Uninomial

RankUninomial <- ('sect' / 'subsect' / 'trib' / 'subtrib' / 'subser' / 'ser.' /
  'subgen' / 'fam' / 'subfam' / 'supertrib') '.'?

Uninomial <- UninomialWord (_ Authorship)?

UninomialWord <- CapWord / TwoLetterGenus

AbbrGenus <- UpperChar LowerChar* '.'

CapWord <- CapWord2 / CapWord1

CapWord1 <- NameUpperChar NameLowerChar NameLowerChar+ '?'?

CapWord2 <- CapWord1 dash (CapWord1 / Word1)

TwoLetterGenus <- ('Ca' / 'Ea' / 'Ge' / 'Ia' / 'Io' / 'Ix' / 'Lo' / 'Oa' /
  'Ra' / 'Ty' / 'Ua' / 'Aa' / 'Ja' / 'Zu' / 'La' / 'Qu' / 'As' / 'Ba')

Word <- !((AuthorPrefix / RankUninomial / Approximation / Word4) SpaceCharEOI)
      (WordApostr / WordStartsWithDigit / Word2 / Word1) &(SpaceCharEOI / '(')

# TODO probably never used
Word1 <- (lASCII dash)? NameLowerChar NameLowerChar+

WordStartsWithDigit <- [123456789] nums? ('.' / dash)? NameLowerChar NameLowerChar
  NameLowerChar NameLowerChar+

Word2 <- NameLowerChar+ dash? NameLowerChar+

WordApostr <- NameLowerChar NameLowerChar* apostr Word1

Word4 <- NameLowerChar+ '.' NameLowerChar

HybridChar <- '×'

ApproxNameIgnored <- .*

Approximation <- ('sp.' _? 'nr.' / 'sp.' _? 'aff.' / 'monst.' /
  '?' / (('spp' / 'nr' / 'sp' / 'aff' / 'species') (&(SpaceCharEOI) / '.')))

Authorship <- (AuthorshipCombo / OriginalAuthorship) &(SpaceCharEOI / ';' / ',')

AuthorshipCombo <- OriginalAuthorshipComb (_? CombinationAuthorship)?

OriginalAuthorship <- AuthorsGroup

OriginalAuthorshipComb <- BasionymAuthorshipYearMisformed / BasionymAuthorship

CombinationAuthorship <- AuthorsGroup

BasionymAuthorshipYearMisformed <- '(' _? AuthorsGroup _? ')' (_? ',')? _? Year

BasionymAuthorship <- BasionymAuthorship1 / BasionymAuthorship2Parens

BasionymAuthorship1 <- '(' _? AuthorsGroup _? ')'

BasionymAuthorship2Parens <- '(' _? '(' _? AuthorsGroup _? ')' _? ')'

AuthorsGroup <- AuthorsTeam (_ (AuthorEmend / AuthorEx) AuthorsTeam)?

AuthorsTeam <- Author (AuthorSep Author)* (_? ','? _? Year)?

AuthorSep <- AuthorSep1 / AuthorSep2

AuthorSep1 <- _? (',' _)? ( '&' / 'et' / 'and' / 'apud') _?

AuthorSep2 <- _? ',' _?

AuthorEx <- ('ex' '.'? / 'in') _

AuthorEmend <- 'emend' '.'? _

Author <- (Author1 / Author2 / UnknownAuthor)

Author1 <- Author2 _? Filius

Author2 <- AuthorWord (_? AuthorWord)*

UnknownAuthor <- '?' / (('auct' / 'anon') (&(SpaceCharEOI) / '.'))

AuthorWord <- !("bold:") (AuthorEtAl /AuthorWord2 / AuthorWord3 / AuthorPrefix)

AuthorEtAl <- 'arg.' / 'et al.{?}' / ('et' / '&') ' al' '.'?

AuthorWord2 <- AuthorWord3 dash AuthorWordSoft

AuthorWord3 <- AuthorPrefixGlued? (AllCapsAuthorWord / CapAuthorWord) '.'?

AuthorWordSoft <- ((AuthorUpperChar (AuthorUpperChar+ / AuthorLowerChar+)) /
  AuthorLowerChar+) '.'?

CapAuthorWord <- AuthorUpperChar AuthorLowerChar*

AllCapsAuthorWord <- AuthorUpperChar AuthorUpperChar+

Filius <- 'f.' / 'fil.' / 'filius'

AuthorPrefixGlued <- 'd\'' / 'O\'' / 'L\''

AuthorPrefix <- AuthorPrefix1 / AuthorPrefix2

AuthorPrefix2 <- ('v.' (_? 'd.')?) / '\'t'

AuthorPrefix1 <- ('ab' / 'af' / 'bis' / 'da' / 'der' / 'des' / 'den' / 'del' /
  'della' / 'dela' / 'de' / 'di' / 'du' / 'el' / 'la' / 'le' / 'ter' / 'van' /
  'd\'' / 'in\'t' / 'zur' / ('von' (_ ('d.'/ 'dem'))?) / ('v' (_'d')?)) &_

AuthorUpperChar <- hASCII / MiscodedChar /
  [ÀÁÂÃÄÅÆÇÈÉÊËÌÍÎÏÐÑÒÓÔÕÖØÙÚÛÜÝĆČĎİĶĹĺĽľŁłŅŌŐŒŘŚŜŞŠŸŹŻŽƒǾȘȚ]


AuthorLowerChar <- lASCII / MiscodedChar /
  [àáâãäåæçèéêëìíîïðñòóóôõöøùúûüýÿāăąćĉčďđ'ēĕėęěğīĭİıĺľłńņňŏőœŕřśşšţťũūŭůűźżžſǎǔǧșțȳß]

Year <- YearRange / YearApprox / YearWithParens / YearWithPage / YearWithDot /
  YearWithChar / YearNum

YearRange <- YearNum dash (nums+ [abcdefghijklmnopqrstuvwxyz?]*)

YearWithDot <-  YearNum '.'

YearApprox  <- '[' _? YearNum _? ']'

YearWithPage <- (YearWithChar / YearNum)  _? ":" _? nums+

YearWithParens <- '(' (YearWithChar / YearNum) ')'

YearWithChar <- YearNum lASCII { p.AddWarn(YearCharWarn) }

YearNum <- [12] [0789] nums (nums / '?') '?'*

NameUpperChar <- UpperChar / UpperCharExtended

UpperCharExtended <- [ÆŒÖ]

UpperChar <- hASCII

NameLowerChar <- LowerChar / LowerCharExtended / MiscodedChar

MiscodedChar <- '�'

LowerCharExtended <- [æœàâåãäáçčéèëíìïňññóòôøõöúùüŕřŗſššşž]

LowerChar <- lASCII

SpaceCharEOI <- _ / !.

# WordBorderChar <- _ / ';' / '.' / ',' / ':' / '(' / ')' / ']'

nums <- [0-9]

lASCII <- [a-z]

hASCII <- [A-Z]

apostr <- '\''

dash <- '-'

_ <- MultipleSpace / SingleSpace

MultipleSpace <- SingleSpace SingleSpace+

SingleSpace <- ' ' / OtherSpace

OtherSpace <- [　 \t\r\n\f\v]
